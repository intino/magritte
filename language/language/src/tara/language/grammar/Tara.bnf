{
	parserClass='tara.intellij.lang.parser.TaraParser'
	parserUtilClass="tara.intellij.lang.parser.TaraParserUtil"
	psiVisitorName="TaraVisitor"
	extends='com.intellij.extapi.psi.ASTWrapperPsiElement'
	implements='tara.intellij.lang.psi.TaraPsiElement'
	psiClassPrefix='Tara'
	psiImplClassSuffix='Impl'
	psiPackage='tara.intellij.lang.psi'
	psiImplPackage='tara.intellij.lang.psi.impl'

	elementTypeHolderClass='tara.intellij.lang.psi.TaraTypes'
	elementTypeClass='tara.intellij.lang.psi.TaraElementType'
	tokenTypeClass='tara.intellij.lang.psi.TaraTokenType'

	psiImplUtilClass='tara.intellij.lang.psi.impl.TaraPsiImplUtil'
}

root ::=COMMENT? NEWLINE* dslDeclaration? imports? (node NEWLINE+)*

dslDeclaration ::= DSL (PROTEO | headerReference) NEWLINE+

imports ::= (anImport NEWLINE*)+

anImport ::= USE headerReference NEWLINE {
	mixin= 'tara.intellij.lang.psi.impl.ImportMixin'
    implements='tara.intellij.lang.psi.Import'
}

doc ::= DOC_LINE+ {
	mixin= 'tara.intellij.lang.psi.impl.DocMixin'
	implements='tara.intellij.lang.psi.Doc'
}

node ::= signature body? {
	pin = 1
	mixin= 'tara.intellij.lang.psi.impl.NodeMixin'
	implements='tara.intellij.lang.psi.Node'
}

signature::= (subNode | metaIdentifier parameters? identifier? parent?) tags? address? {
	pin = 1
	mixin= 'tara.intellij.lang.psi.impl.SignatureMixin'
	implements='tara.intellij.lang.psi.Signature'
}

address::= ADDRESS_VALUE
{pin = 1}

private subNode ::= SUB parameters? identifier
{pin = 1}

private parent ::= EXTENDS identifierReference
{pin = 1}

parameters ::= LEFT_PARENTHESIS (explicitParameters | implicitParameters)? RIGHT_PARENTHESIS {
	pin = 1
	mixin = 'tara.intellij.lang.psi.impl.ParametersMixin'
	implements = 'tara.intellij.lang.psi.Parameters'
}

private explicitParameters ::= explicitParameter (COMMA explicitParameter)*
{ pin = 2}

private implicitParameters ::= implicitParameter (COMMA implicitParameter)*
{ pin = 1}

explicitParameter ::= identifier EQUALS value {
    pin = 2
	mixin = 'tara.intellij.lang.psi.impl.ParameterMixin'
	implements = 'tara.intellij.lang.psi.Parameter'
}

implicitParameter ::= value {
    mixin = 'tara.intellij.lang.psi.impl.ParameterMixin'
    implements = 'tara.intellij.lang.psi.Parameter'
}

body ::= (NEW_LINE_INDENT NEWLINE?| INLINE) (nodeConstituents NEWLINE+)+ DEDENT {
	pin = 1
	mixin = 'tara.intellij.lang.psi.impl.BodyMixin'
	implements = 'tara.intellij.lang.psi.Body'
}

private nodeConstituents ::= varInit | variable | node | facetTarget | facetApply | nodeReference | doc

nodeReference ::= HAS identifierReference tags? {
	pin = 2
	mixin = 'tara.intellij.lang.psi.impl.NodeReferenceMixin'
	implements = 'tara.intellij.lang.psi.NodeReference'
}

variable ::= VAR variableType attributeType? (LIST | count)? identifier (EQUALS value measureValue?)? flags? (NEW_LINE_INDENT (doc NEWLINE+)+ DEDENT)? {
	pin = 1
	mixin = 'tara.intellij.lang.psi.impl.VariableMixin'
	implements = 'tara.intellij.lang.psi.Variable'
}

variableType::= NATURAL_TYPE
                | NATIVE_TYPE
                | TUPLE_TYPE
                | INT_TYPE
                | BOOLEAN_TYPE
                | STRING_TYPE
                | DATE_TYPE
                | RATIO_TYPE
                | DOUBLE_TYPE
                | MEASURE_TYPE_KEY
                | WORD_KEY
                | RESOURCE_KEY
                | identifierReference

count ::= LEFT_SQUARE NATURAL_VALUE_KEY RIGHT_SQUARE

attributeType ::= COLON contract
{pin=1}

contract   ::= (LEFT_SQUARE (MEASURE_VALUE_KEY | identifier)+ RIGHT_SQUARE) | (MEASURE_VALUE_KEY | identifier) {
	implements = 'tara.intellij.lang.psi.Contract'
	mixin = 'tara.intellij.lang.psi.impl.ContractMixin'
}

value ::= stringValue+
        | booleanValue+
        | tupleValue+
        | naturalValue+ measureValue?
        | integerValue+ measureValue?
        | doubleValue+  measureValue?
        | expression+
        | emptyField
        | instanceName+
        | identifierReference+
{
mixin = 'tara.intellij.lang.psi.impl.ValueMixin'
    implements = 'tara.intellij.lang.psi.Value'
}

stringValue ::= NEWLINE? (QUOTE_BEGIN CHARACTER* QUOTE_END) {
	mixin = 'tara.intellij.lang.psi.impl.StringMixin'
    implements = 'tara.intellij.lang.psi.StringValue'
}

expression  ::= NEWLINE? (EXPRESSION_BEGIN CHARACTER* EXPRESSION_END) {
	mixin = 'tara.intellij.lang.psi.impl.ExpressionMixin'
    implements = 'tara.intellij.lang.psi.Expression'
}

booleanValue    ::= BOOLEAN_VALUE_KEY
tupleValue      ::= stringValue COLON doubleValue
naturalValue    ::= NATURAL_VALUE_KEY
integerValue    ::= NATURAL_VALUE_KEY | NEGATIVE_VALUE_KEY
doubleValue     ::= NATURAL_VALUE_KEY | NEGATIVE_VALUE_KEY | DOUBLE_VALUE_KEY
instanceName    ::= ADDRESS_VALUE
measureValue    ::= identifier | MEASURE_VALUE_KEY

facetApply ::= AS metaIdentifier parameters? (WITH metaIdentifier)? body? {
	pin = 1
	mixin = 'tara.intellij.lang.psi.impl.FacetApplyMixin'
    implements = 'tara.intellij.lang.psi.FacetApply'
}

facetTarget ::= ON (identifierReference | ANY) constraint? body? {
	pin = 1
	mixin = 'tara.intellij.lang.psi.impl.FacetTargetMixin'
	implements = 'tara.intellij.lang.psi.FacetTarget'
}
constraint::= WITH identifierReference (COMMA identifierReference)*
{pin = 1}

tags ::= flags? annotations? {
	pin = 1
	mixin = 'tara.intellij.lang.psi.impl.TagsMixin'
	implements = 'tara.intellij.lang.psi.Tags'
}

flags::= IS flag+ {
	pin = 1
	mixin = 'tara.intellij.lang.psi.impl.FlagsMixin'
	implements = 'tara.intellij.lang.psi.Flags'
}

flag ::= ABSTRACT | TERMINAL | MAIN
	| SINGLE | REQUIRED | PRIVATE
	| FACET | FEATURE | PROTOTYPE | ENCLOSED | FINAL {
	mixin = 'tara.intellij.lang.psi.impl.FlagMixin'
	implements = 'tara.intellij.lang.psi.Flag'
}

annotations ::= INTO annotation+ {
	pin = 1
	mixin = 'tara.intellij.lang.psi.impl.AnnotationsMixin'
	implements = 'tara.intellij.lang.psi.Annotations'
}
annotation ::= TERMINAL
	| SINGLE | REQUIRED
	| FACET | FEATURE | PROTOTYPE | ENCLOSED | MAIN {
	mixin = 'tara.intellij.lang.psi.impl.AnnotationMixin'
	implements = 'tara.intellij.lang.psi.Annotation'
}

varInit ::= identifier EQUALS value {
    pin = 2
	mixin = 'tara.intellij.lang.psi.impl.VarInitMixin'
	implements = 'tara.intellij.lang.psi.Parameter'
}

emptyField ::= EMPTY_REF

headerReference ::= hierarchy* identifier {
	pin = 2
	mixin = 'tara.intellij.lang.psi.impl.IdentifierReferenceMixin'
	implements = 'tara.intellij.lang.psi.HeaderReference'
}

identifierReference ::= hierarchy* identifier {
	pin = 2
	mixin = 'tara.intellij.lang.psi.impl.IdentifierReferenceMixin'
	implements = 'tara.intellij.lang.psi.IdentifierReference'
}

private hierarchy ::= identifier DOT
{pin = 2}

identifier ::=  IDENTIFIER_KEY {
	mixin = 'tara.intellij.lang.psi.impl.IdentifierMixin'
	implements = 'tara.intellij.lang.psi.Identifier'
}

metaIdentifier ::= METAIDENTIFIER_KEY | IDENTIFIER_KEY {
	mixin = 'tara.intellij.lang.psi.impl.MetaIdentifierMixin'
	implements = 'tara.intellij.lang.psi.MetaIdentifier'
}