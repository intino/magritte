{
	parserClass='siani.tara.intellij.lang.parser.TaraParser'
	parserUtilClass="siani.tara.intellij.lang.parser.TaraParserUtil"
	psiVisitorName="TaraVisitor"
	extends='com.intellij.extapi.psi.ASTWrapperPsiElement'
	implements='siani.tara.intellij.lang.psi.TaraPsiElement'
	psiClassPrefix='Tara'
	psiImplClassSuffix='Impl'
	psiPackage='siani.tara.intellij.lang.psi'
	psiImplPackage='siani.tara.intellij.lang.psi.impl'

	elementTypeHolderClass='siani.tara.intellij.lang.psi.TaraTypes'
	elementTypeClass='siani.tara.intellij.lang.psi.TaraElementType'
	tokenTypeClass='siani.tara.intellij.lang.psi.TaraTokenType'

	psiImplUtilClass='siani.tara.intellij.lang.psi.impl.TaraPsiImplUtil'
}

root ::= NEWLINE* header? NEWLINE+ (concept  NEWLINE+)*

header ::= box?  imports?

box ::= BOX_KEY headerReference

private imports ::= anImport+

anImport ::= NEWLINE+ USE_KEY headerReference (AS METAMODEL)? {
	mixin= 'siani.tara.intellij.lang.psi.impl.ImportMixin'
    implements='siani.tara.intellij.lang.psi.Import'
}

doc::= DOC_LINE+ {
	mixin= 'siani.tara.intellij.lang.psi.impl.DocMixin'
	implements='siani.tara.intellij.lang.psi.Doc'
}

concept ::= doc? signature annotationsAndFacets? body? {
	pin = 2
	mixin= 'siani.tara.intellij.lang.psi.impl.ConceptMixin'
	implements='siani.tara.intellij.lang.psi.Concept'
}
//simplify
signature::= (SUB identifier) | (metaIdentifier parameters? identifier parent?) | (metaIdentifier parameters?)  {
	pin=1
	mixin= 'siani.tara.intellij.lang.psi.impl.SignatureMixin'
	implements='siani.tara.intellij.lang.psi.Signature'
}

private parent ::= EXTENDS identifierReference
{pin = 1}

parameters ::= LEFT_PARENTHESIS (explicitParameters | implicitParameters)? RIGHT_PARENTHESIS {
	pin = 3
	mixin = 'siani.tara.intellij.lang.psi.impl.ParametersMixin'
	implements = 'siani.tara.intellij.lang.psi.Parameters'
}

private explicitParameters ::= explicitParameter (COMMA explicitParameter)*
{ pin=2}

private implicitParameters ::= implicitParameter (COMMA implicitParameter)*
{ pin=1}

explicitParameter ::= identifier EQUALS parameterValue {
    pin = 2
	mixin = 'siani.tara.intellij.lang.psi.impl.ParameterMixin'
	implements = 'siani.tara.intellij.lang.psi.Parameter'
}

implicitParameter ::= parameterValue {
    mixin = 'siani.tara.intellij.lang.psi.impl.ParameterMixin'
    implements = 'siani.tara.intellij.lang.psi.Parameter'
}

parameterValue ::= identifierReference+
				| stringValue+
		        | booleanValue+
		        | naturalValue+ measure?
		        | integerValue+ measure?
		        | doubleValue+  measure?
		        | dateValue+
		        | codeValue+
		        | coordinateValue+
		        | metaWord
		        | empty

metaWord ::= metaIdentifier metaWordNames*
{pin = 1}
private metaWordNames ::= DOT identifier
{pin = 1}

empty::=

body ::= NEW_LINE_INDENT (conceptConstituents NEWLINE+)+ DEDENT {
	pin = 2
	mixin= 'siani.tara.intellij.lang.psi.impl.BodyMixin'
	implements='siani.tara.intellij.lang.psi.Body'
}

private conceptConstituents ::= attribute | concept | varInit | annotationsAndFacets | facetTarget | conceptReference

conceptReference ::= doc? HAS identifierReference identifier?

attribute ::= doc? VAR (refAttribute | naturalAttribute | integerAttribute | doubleAttribute | booleanAttribute | stringAttribute |
			dateAttribute | coordinateAttribute | resource | referenceAttribute | word) annotationsAndFacets? {
	pin=2
	mixin= 'siani.tara.intellij.lang.psi.impl.AttributeMixin'
	implements='siani.tara.intellij.lang.psi.Attribute'
}
private resource             ::= RESOURCE_KEY attributeType IDENTIFIER_KEY                                                  {pin=1}
private coordinateAttribute  ::= COORDINATE_TYPE  LIST? IDENTIFIER_KEY (EQUALS (coordinateValue | emptyField))?             {pin=1}
private refAttribute         ::= PORT_TYPE        LIST? IDENTIFIER_KEY (EQUALS (codeValue  | emptyField))?                  {pin=1}
private booleanAttribute     ::= BOOLEAN_TYPE LIST?  IDENTIFIER_KEY (EQUALS (booleanValue | emptyField))?                   {pin=1}
private stringAttribute      ::= STRING_TYPE  LIST?  IDENTIFIER_KEY (EQUALS (stringValue  | emptyField))?                   {pin=1}
private naturalAttribute     ::= NATURAL_TYPE attributeType? LIST?  IDENTIFIER_KEY (EQUALS (naturalValue measure? | emptyField))?    {pin=1}
private integerAttribute     ::= INT_TYPE     attributeType? LIST?  IDENTIFIER_KEY (EQUALS (integerValue measure? | emptyField))?    {pin=1}
private doubleAttribute      ::= DOUBLE_TYPE  attributeType? LIST?  IDENTIFIER_KEY (EQUALS (doubleValue  measure? | emptyField))?    {pin=1}
private dateAttribute        ::= DATE_TYPE    LIST?  IDENTIFIER_KEY (EQUALS (dateValue | emptyField))?                      {pin=1}
private referenceAttribute   ::= identifierReference LIST? IDENTIFIER_KEY (EQUALS emptyField)? {
	pin = 1
	mixin= 'siani.tara.intellij.lang.psi.impl.ReferenceStatementMixin'
	implements='siani.tara.intellij.lang.psi.ReferenceStatement'
}
word ::= WORD_KEY IDENTIFIER_KEY NEW_LINE_INDENT (IDENTIFIER_KEY STAR? NEWLINE)+ DEDENT {
	pin = 1
	mixin = 'siani.tara.intellij.lang.psi.impl.WordMixin'
	implements ='siani.tara.intellij.lang.psi.Word'
}
attributeType   ::= COLON IDENTIFIER_KEY
stringValue     ::= STRING_VALUE_KEY  | STRING_MULTILINE_VALUE_KEY
booleanValue    ::= BOOLEAN_VALUE_KEY
naturalValue    ::= NATURAL_VALUE_KEY
integerValue    ::= NATURAL_VALUE_KEY | NEGATIVE_VALUE_KEY
doubleValue     ::= NATURAL_VALUE_KEY | NEGATIVE_VALUE_KEY | DOUBLE_VALUE_KEY
codeValue       ::= CODE_VALUE_KEY
dateValue       ::= DATE_VALUE_KEY
coordinateValue ::= COORDINATE_VALUE_KEY

measure ::= IDENTIFIER_KEY | DOLLAR | EURO | PERCENTAGE | GRADE

annotationsAndFacets ::= IS (annotations+ | facetApply) {
	pin = 1
}
facetApply ::= metaIdentifier parameters? (WITH metaIdentifier)?
{pin = 2}
facetTarget ::= ON identifierReference ALWAYS? body?
{pin = 1}

annotations ::= (PRIVATE | TERMINAL | SINGLE | REQUIRED | NAMED | COMPONENT | FACET | INTENTION | PROPERTY)+ {
	pin = 1
	mixin = 'siani.tara.intellij.lang.psi.impl.AnnotationsMixin'
	implements = 'siani.tara.intellij.lang.psi.Annotations'
}


varInit ::= IDENTIFIER_KEY EQUALS ( emptyField
								 | identifierReference+
								 | stringValue+
                                 | booleanValue+
                                 | dateValue+
                                 | codeValue+
                                 | coordinateValue+
                                 | naturalValue+ measure?
                                 | integerValue+ measure?
                                 | doubleValue+  measure?)
{pin=2}
emptyField ::= EMPTY_REF

headerReference ::= hierarchy* identifier {
	pin=2
	mixin= 'siani.tara.intellij.lang.psi.impl.IdentifierReferenceMixin'
	implements='siani.tara.intellij.lang.psi.HeaderReference'
}

identifierReference ::= hierarchy* identifier {
	pin=2
	mixin= 'siani.tara.intellij.lang.psi.impl.IdentifierReferenceMixin'
	implements='siani.tara.intellij.lang.psi.IdentifierReference'
}

private hierarchy ::= identifier DOT
{pin=2}

identifier ::=  IDENTIFIER_KEY {
	mixin= 'siani.tara.intellij.lang.psi.impl.IdentifierMixin'
	implements='siani.tara.intellij.lang.psi.Identifier'
}

metaIdentifier ::= METAIDENTIFIER_KEY | IDENTIFIER_KEY {
	mixin= 'siani.tara.intellij.lang.psi.impl.MetaIdentifierMixin'
	implements='siani.tara.intellij.lang.psi.MetaIdentifier'
}