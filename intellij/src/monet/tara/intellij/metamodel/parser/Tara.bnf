{
  parserClass='monet.tara.intellij.metamodel.parser.TaraParser'
  parserUtilClass="monet.tara.intellij.metamodel.parser.TaraParserUtil"

  extends='com.intellij.extapi.psi.ASTWrapperPsiElement'

  psiClassPrefix='Tara'
  psiImplClassSuffix='Impl'
  psiPackage='monet.tara.intellij.metamodel.psi'
  psiImplPackage='monet.tara.intellij.metamodel.psi.impl'

  elementTypeHolderClass='monet.tara.intellij.metamodel.psi.TaraTypes'
  elementTypeClass='monet.tara.intellij.metamodel.psi.TaraElementType'
  tokenTypeClass='monet.tara.intellij.metamodel.psi.TaraTokenType'

  psiImplUtilClass='monet.tara.intellij.metamodel.psi.impl.TaraPsiImplUtil'
}

root ::= (concept | NEWLINE)*

concept ::= doc? signature annotations? body?
{ mixin= 'monet.tara.intellij.metamodel.psi.impl.ConceptMixin'
implements='monet.tara.intellij.metamodel.psi.Concept'
}

signature ::= CONCEPT_KEY referenceIdentifier?  (polymorphic | modifier? morph?)  AS identifier
{ mixin= 'monet.tara.intellij.metamodel.psi.impl.SignatureMixin'
implements='monet.tara.intellij.metamodel.psi.Signature'
}

body ::= NEW_LINE_INDENT (conceptConstituents NEWLINE+)+ DEDENT
{ mixin= 'monet.tara.intellij.metamodel.psi.impl.BodyMixin'
implements='monet.tara.intellij.metamodel.psi.Body'
}

private conceptConstituents ::= attribute
		                      | referenceStatement
		                      | word
		                      | concept
		                      | conceptInjection

referenceStatement::= VAR referenceIdentifier LIST? variableNames
conceptInjection::= NEW referenceIdentifier annotations?
attribute::= VAR     UID_TYPE IDENTIFIER_KEY (ASSIGN stringValue)?
           | VAR     INT_TYPE (variableNames | IDENTIFIER_KEY ASSIGN integerValue | LIST IDENTIFIER_KEY (ASSIGN integerList)?)
           | VAR  DOUBLE_TYPE (variableNames | IDENTIFIER_KEY ASSIGN doubleValue | LIST IDENTIFIER_KEY (ASSIGN doubleList)?)
           | VAR NATURAL_TYPE (variableNames | IDENTIFIER_KEY ASSIGN naturalValue | LIST IDENTIFIER_KEY (ASSIGN naturalList)?)
           | VAR BOOLEAN_TYPE (variableNames | IDENTIFIER_KEY ASSIGN booleanValue | LIST IDENTIFIER_KEY (ASSIGN booleanList)?)
           | VAR  STRING_TYPE (variableNames | IDENTIFIER_KEY ASSIGN stringValue | LIST IDENTIFIER_KEY (ASSIGN stringList)?)
{mixin= 'monet.tara.intellij.metamodel.psi.impl.AttributeMixin'
implements='monet.tara.intellij.metamodel.psi.Attribute'}

word::= VAR WORD_KEY IDENTIFIER_KEY NEW_LINE_INDENT (IDENTIFIER_KEY NEWLINE)+ DEDENT

stringValue  ::= STRING_VALUE_KEY
booleanValue ::= BOOLEAN_VALUE_KEY
integerValue ::= NATURAL_VALUE_KEY | NEGATIVE_VALUE_KEY
doubleValue  ::= NATURAL_VALUE_KEY | NEGATIVE_VALUE_KEY | DOUBLE_VALUE_KEY
naturalValue ::= NATURAL_VALUE_KEY

stringList ::= LEFT_SQUARE STRING_VALUE_KEY+ RIGHT_SQUARE;
booleanList::= LEFT_SQUARE BOOLEAN_VALUE_KEY+ RIGHT_SQUARE;
integerList::= LEFT_SQUARE (NATURAL_VALUE_KEY | NEGATIVE_VALUE_KEY)+ RIGHT_SQUARE;
doubleList ::= LEFT_SQUARE (NATURAL_VALUE_KEY | NEGATIVE_VALUE_KEY | DOUBLE_VALUE_KEY)+ RIGHT_SQUARE;
naturalList::= LEFT_SQUARE NATURAL_VALUE_KEY+ RIGHT_SQUARE;

annotations ::= OPEN_AN (MULTIPLE | OPTIONAL | HAS_CODE | EXTENSIBLE| SINGLETON | ROOT)+ CLOSE_AN {methods=[getAnnotations]}

variableNames::= IDENTIFIER_KEY (COMMA IDENTIFIER_KEY)*;

modifier::= ABSTRACT
          | FINAL

polymorphic::= POLYMORPHIC_KEY
morph::= MORPH_KEY

doc::= DOC_LINE+

referenceIdentifier::= identifier (DOT identifier)*

identifier::=  IDENTIFIER_KEY
{mixin= 'monet.tara.intellij.metamodel.psi.impl.IdentifierMixin'}