{
  parserClass='monet.tara.metamodel.parser.TaraParser'
  parserUtilClass="monet.tara.metamodel.parser.TaraParserUtil"

  extends='com.intellij.extapi.psi.ASTWrapperPsiElement'

  psiClassPrefix='Tara'
  psiImplClassSuffix='Impl'
  psiPackage='monet.tara.metamodel.psi'
  psiImplPackage='monet.tara.metamodel.psi.impl'

  elementTypeHolderClass='monet.tara.metamodel.psi.TaraTypes'
  elementTypeClass='monet.tara.metamodel.psi.TaraElementType'
  tokenTypeClass='monet.tara.metamodel.psi.TaraTokenType'

  psiImplUtilClass='monet.tara.metamodel.psi.impl.TaraPsiImplUtil'
}

root ::= (COMMENT|conceptDefinition)+

conceptDefinition ::= conceptSignature conceptBody? {mixin='monet.tara.metamodel.psi.impl.TaraNamedElementImpl' implements='monet.tara.metamodel.psi.TaraNamedElement' methods=[getName setName getNameIdentifier]}

conceptSignature ::= CONCEPT MODIFIERS? identifier (IS identifier)? ANNOTATION*

conceptBody::= INDENT statement+ DEDENT

statement::= property | child | referenceStatement | intention

property::=primitive_type identifier (ASSIGN  primitive)? ANONYMOUS?

child::=HAS identifier RANGE? ANNOTATION* conceptBody*

identifier ::= IDENTIFIER_KEY {methods=[getIdentifier]}

referenceStatement::=REF identifier IDENTIFIER_KEY

intention::=USE identifier COLON identifier (LEFT_P identifier RIGHT_P)?

primitive_type ::=  ID_TYPE|INT_TYPE|STRING_TYPE|DOUBLE_TYPE

primitive ::=  INT|DOUBLE|STRING