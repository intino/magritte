{
  parserClass='monet.tara.metamodel.TaraParser'
  parserUtilClass="monet.tara.metamodel.parser.TaraParserUtil"

  extends='com.intellij.extapi.psi.ASTWrapperPsiElement'

  psiClassPrefix='Tara'
  psiImplClassSuffix='Impl'
  psiPackage='monet.tara.metamodel.psi'
  psiImplPackage='monet.tara.metamodel.psi.impl'

  elementTypeHolderClass='monet.tara.metamodel.psi.TaraTypes'
  elementTypeClass='monet.tara.metamodel.psi.TaraElementType'
  tokenTypeClass='monet.tara.metamodel.psi.TaraTokenType'

  psiImplUtilClass='monet.tara.metamodel.psi.impl.TaraPsiImplUtil'
}

root ::=(COMMENT|conceptDefinition)+

conceptDefinition ::= conceptSignature conceptBody?  {mixin='monet.tara.metamodel.psi.impl.TaraNamedElementImpl' implements='monet.tara.metamodel.psi.TaraNamedElement' methods=[getIdentifier getName setName getNameIdentifier]}

conceptSignature ::= CONCEPT MODIFIERS? IDENTIFIER (IS IDENTIFIER)? ANNOTATION*

private conceptBody::= INDENT statement+ DEDENT

private statement::= property | child | reference | intention

property::=primitive_type IDENTIFIER (ASSIGN  primitive)? ANONYMOUS?

child::=HAS IDENTIFIER RANGE? ANNOTATION* conceptBody*

reference::=REF IDENTIFIER IDENTIFIER {methods=[getIdentifier]}

intention::=USE IDENTIFIER COLON IDENTIFIER PARAMETER?

primitive_type ::=  ID_TYPE|INT_TYPE|STRING_TYPE|DOUBLE_TYPE

primitive ::=  INT|DOUBLE|STRING